plugins {
    java
    kotlin("multiplatform") version "1.4-M2"
    kotlin("kapt") version "1.3.72"
    kotlin("plugin.serialization") version "1.3.70"
    id("org.jlleitschuh.gradle.ktlint") version "9.2.1"
}

group = "org.wildfly.halos"
version = "0.0.1"
val fritzVersion = "0.5"

repositories {
    maven("https://dl.bintray.com/kotlin/kotlin-eap")
    maven("https://kotlin.bintray.com/kotlin-js-wrappers/")
    maven("https://kotlin.bintray.com/kotlinx")
    mavenCentral()
    jcenter()
}

ktlint {
    disabledRules.set(setOf("import-ordering"))
}

kotlin {

    jvm()
    js {
        browser {
            runTask {
                devServer = org.jetbrains.kotlin.gradle.targets.js.webpack.KotlinWebpackConfig.DevServer(
                    port = 9999,
                    contentBase = listOf("$buildDir/processedResources/js/main")
                )
            }
        }
        binaries.executable()
    }

    sourceSets {
        val commonMain by getting {
            dependencies {
                implementation(kotlin("stdlib"))
                implementation("dev.fritz2:core:$fritzVersion")
                configurations.get("kapt").dependencies.add(compileOnly("dev.fritz2:lenses-annotation-processor:$fritzVersion"))
            }
            tasks.getByName("compileKotlinJs").dependsOn("kaptKotlinJvm")
            tasks.getByName("compileKotlinMetadata").dependsOn("kaptKotlinJvm")
            kotlin.srcDir("$buildDir/generated/source/kaptKotlin/main")
        }
        val generated by creating {
            dependsOn(commonMain)
            kotlin.srcDir("$buildDir/generated/source/kaptKotlin")
        }
        val jvmMain by getting {
            dependsOn(generated)
            dependencies {
            }
        }
        val jvmTest by getting {
            dependsOn(generated)
        }
        val jsMain by getting {
            dependsOn(generated)
            dependencies {
                implementation("org.jetbrains:kotlin-extensions:1.0.1-pre.109-kotlin-1.3.72")
                implementation("io.github.microutils:kotlin-logging-js:1.7.9")
                implementation("org.jetbrains:kotlin-react:16.13.1-pre.109-kotlin-1.3.72")
                implementation("org.jetbrains:kotlin-react-dom:16.13.1-pre.109-kotlin-1.3.72")
                implementation(npm("@patternfly/patternfly", "4.10.31"))
                implementation(npm("@patternfly/react-charts", "6.3.9"))
                // dev dependencies
                implementation(npm("file-loader", "6.0.0"))
            }
        }
        val jsTest by getting {
            dependsOn(generated)
        }
    }
}
